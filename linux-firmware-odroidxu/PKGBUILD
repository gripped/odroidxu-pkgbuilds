# $Id: PKGBUILD 197749 2013-10-29 09:58:12Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Modifier; gripped - forum.odroid.com

pkgname=linux-firmware-odroidxu
# Commit date + git rev-parse --short origin/master
epoch=1
pkgver=20140316.dec41bc
pkgrel=1
pkgdesc="Firmware files for Linux"
makedepends=('git')
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
conflicts=('linux-firmware-git'
           'kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'amd-ucode')
replaces=('kernel26-firmware'
          'ar9170-fw'
          'iwlwifi-1000-ucode'
          'iwlwifi-3945-ucode'
          'iwlwifi-4965-ucode'
          'iwlwifi-5000-ucode'
          'iwlwifi-5150-ucode'
          'iwlwifi-6000-ucode'
          'rt2870usb-fw'
          'rt2x00-rt61-fw'
          'rt2x00-rt71w-fw'
          'amd-ucode')
provides=('linux-firmware')
options=(!strip)
source=('git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git#commit=dec41bce44e0dff6a2c3358a958fadf22bf58858')
md5sums=('SKIP')

package() {
  cd "${srcdir}/linux-firmware"
  ln -s s5p-mfc/s5p-mfc-v6.fw mfc_fw.bin
  # remove not needed Makefile #35236
  rm Makefile
  install -d -m755 "${pkgdir}/usr/lib/firmware"
  cp -a * "${pkgdir}/usr/lib/firmware/"
  install -d -m755 "${pkgdir}/usr/share/licenses/linux-firmware/"
  rm -f "${pkgdir}/usr/lib/firmware/"{README*,configure,GPL*}
  mv "${pkgdir}/usr/lib/firmware"/{LICEN*,WHENCE} "${pkgdir}/usr/share/licenses/linux-firmware/"
}
# vim:set ts=2 sw=2 et: