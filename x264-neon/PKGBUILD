# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure needs --disable-asm to build on ARM

pkgname=x264-neon
_pkgname=x264
pkgver=142.20140311
pkgrel=1
epoch=1
pkgdesc='Library for encoding H264/AVC video streams'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git')
provides=('x264')
conflicts=('x264')
source=(git://git.videolan.org/x264.git#commit=aff928d2)
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _date=$(git log -1 --format="%cd" --date=short | tr -d -)

  echo ${_ver}.${_date}
}

build() {
  cd $_pkgname

  ./configure --prefix=/usr \
    --enable-shared \
    --enable-pic 

  make
}

package() {
  make -C $_pkgname DESTDIR="$pkgdir" install
}
